/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

buildscript {
    String rootGroupId = project.ext.rootGroupId = "name.remal.gradle-plugins.${rootProject.name}"
    String rootArtifactId = project.ext.rootArtifactId = rootProject.name
    String rootSnapshotVersion = project.ext.rootSnapshotVersion = '1-SNAPSHOT'
    dependencies {
        //classpath("$rootGroupId:$rootArtifactId:$rootSnapshotVersion") { version { strictly(rootSnapshotVersion) } }
        classpath 'name.remal.gradle-plugins.toolkit:build-logic:0.69.7'
    }
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

allprojects {
    group = project.rootGroupId
    version = project.rootSnapshotVersion
}

apply plugin: 'name.remal.toolkit.build-logic'

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

subprojects {
    pluginManager.withPlugin('maven-publish') {
        publishing.publications.withType(MavenPublication).configureEach {
            artifactId = "${rootProject.name}-${project.name}"
        }
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

apply plugin: 'java-gradle-plugin'
apply plugin: 'name.remal.generate-sources'

dependencies {
    optional project(':api')
    optional 'org.ow2.asm:asm-util'

    implementation 'org.ow2.asm:asm-tree'


    testImplementation 'com.google.jimfs:jimfs:1.3.0'
}

gradlePlugin {
    plugins {
        'name.remal.build-time-constants' {
            id = 'name.remal.build-time-constants'
            implementationClass = 'name.remal.gradle_plugins.build_time_constants.jvm.BuildTimeConstantsJvmPlugin'
            displayName = 'Insert compile-time constants into JVM class files'
            description = property('repository-description')
        }
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

Provider<MavenPublication> apiPublication = provider {
    project(':api').publishing.publications.withType(MavenPublication).collect().first()
}
Provider<String> apiGroup = project.ext['apiGroup'] = provider { apiPublication.get().groupId }
Provider<String> apiArtifactId = project.ext['apiArtifactId'] = provider { apiPublication.get().artifactId }
Provider<String> apiVersion = project.ext['apiVersion'] = provider { apiPublication.get().version }

project.ext['apiNotation'] = provider { "${apiGroup.get()}:${apiArtifactId.get()}:${apiVersion.get()}" }
project.ext['apiNotationWithoutVersion'] = provider { "${apiGroup.get()}:${apiArtifactId.get()}" }

tasks.named('generateJava') {
    inputs.property('groupId', apiGroup)
    inputs.property('artifactId', apiArtifactId)
    inputs.property('version', apiVersion)
    classFile(project.calculateBaseJavaPackage() + '.jvm', 'BuildTimeConstantsApiBuildInfo') {
        it.writePackage()
        it.println("")
        it.writeImport("org.jetbrains.annotations.ApiStatus.Internal")
        it.println("")
        it.println("@Internal")
        it.writeBlock("interface ${it.simpleName}") {
            it.println("String BUILD_TIME_CONSTANTS_API_GROUP = \"${it.escapeJava(apiGroup.get())}\";")
            it.println("String BUILD_TIME_CONSTANTS_API_ARTIFACT_ID = \"${it.escapeJava(apiArtifactId.get())}\";")
            it.println("String BUILD_TIME_CONSTANTS_API_VERSION = \"${it.escapeJava(apiVersion.get())}\";")
        }
    }
}
